buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    jcenter()
}

dependencies {
    compile("com.github.grooviter:asteroid:$asteroid") {
        exclude module: "groovy-all"
        exclude module: "groovy"
    }
    compile "org.codehaus.groovy:groovy:$groovy_all"
    compile "org.codehaus.groovy:groovy-sql:$groovy_all"
    compile "org.codehaus.groovy:groovy-cli-picocli:$groovy_all"
    compile "org.fusesource.jansi:jansi:$jansiVersion"
    compile "org.yaml:snakeyaml:$snake_yaml"
    compile "org.zeroturnaround:zt-zip:$zt"

    testCompile "com.h2database:h2:$h2"
    testCompile "org.spockframework:spock-core:$spock_core"
    testCompile("com.github.grooviter:asteroid:$asteroid") {
        exclude module: "groovy-all"
        exclude module: "groovy"
    }
}

codenarc {
    configFile file('src/main/codenarc/config.groovy')
    toolVersion "1.2"
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            groupId    releaseGroup
            artifactId 'carbon'
            version    releaseVersion

            from components.java
        }
    }
}

bintray {
 /* Normally taken from ~/.gradle/gradle.properties */
    user         = project.properties.bintrayUser
    key          = project.properties.bintrayKey
    publish      = false
    publications = ['mavenCustom']
    pkg {
        repo            = 'maven'
        name            = 'carbon'
        userOrg         = 'grooviter'
        desc            =  releaseDesc
        websiteUrl      = 'http://grooviter.github.io/carbon/'
        issueTrackerUrl = 'https://github.com/grooviter/carbon/issues'
        licenses        = ['Apache-2.0']
        vcsUrl          = 'https://github.com/grooviter/carbon.git'
        labels          = ['groovy', 'ast']
        version {
            name      = releaseVersion
            desc      = 'Carbon Release'
            released  = new Date()
            vcsTag    = releaseVersion
        }
    }
}

task('console', dependsOn:'classes', type: JavaExec) {
    group = 'help'
    description 'starts a Groovy console with the classpath of the current project'
    main='groovy.ui.Console'
    project.dependencies.add("compile", 'org.codehaus.groovy:groovy-console:2.4.12')
    classpath = sourceSets.main.runtimeClasspath
}
